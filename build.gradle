import java.time.LocalDateTime
import java.time.format.DateTimeFormatter

buildscript {
  repositories { jcenter() }
}

plugins {
  id "java"
  id "eclipse"
  id "groovy"
  id "com.github.johnrengelman.shadow" version "7.0.0"
  id 'org.jreleaser' version '1.5.0'
}


apply plugin: 'application'
mainClassName = 'jreleaser.JReleaserTest'
applicationDefaultJvmArgs = []


compileJava {
  sourceCompatibility = '11'
  targetCompatibility = '11'
}


//MAJOR.MINOR.REVISION.BUILDNUMBER
class MyVersion {
  String major="4"
  String minor="0"
  String revision="1"
  String build=LocalDateTime.now().format(DateTimeFormatter.ofPattern("yyyyMMddHHmm"))
  String toString() {
    return "$major.$minor.$revision-${build}"
  }
}

//project.name="myproject"
project.group="my.group"
project.version = new MyVersion().toString()
project.description="jreleaser example application"
//project.copyright="no rights reserved"

sourceSets {
  main {
    resources {srcDirs= ["src/main/resources"]}
  }
}

repositories { jcenter() }

dependencies {
  implementation 'org.codehaus.groovy:groovy-all:3.0.6'
  implementation 'org.apache.logging.log4j:log4j-core:2.10.0'
  implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.10.0'
}


jreleaser {
  project {
    authors = ['dumischbaenger']
    license = 'no rights reserved'
    links {
        homepage = 'https://mysuperwebsite.com/'
    }
    inceptionYear = '2023'
  }

  distributions {
    jreleasertest {
       artifact {
           path = 'build/distributions/{{distributionName}}-{{projectVersion}}.zip'
       }
       artifact {
           path = 'build/libs/{{distributionName}}-{{projectVersion}}-all.jar'
       }
    }
  }

  release {
    github {
      enabled = true
      repoOwner = 'dumischbaenger'
      name = 'jreleasertest'
      username = 'dumischbaenger'
//      overwrite = true


      prerelease {
        enabled = false
        pattern = '.*-pre'
      }


      milestone {
        close = true
        name = '{{tagName}}'
      }

      issues {
        enabled = true
        comment = 'Released in {{tagName}} -> {{releaseNotesUrl}}'
        applyMilestone = 'ALWAYS'

        label {
          name = 'released'
          color = '#FF0000'
          description = 'Issue has been released'
        }
      }
    }
  }
}

//BD zusaetzliche Task Dependencies
jreleaserRelease.dependsOn shadowJar

//BD Task Dependencies ausgeben
gradle.taskGraph.whenReady {taskGraph ->
  println "Tasks"
  taskGraph.getAllTasks().eachWithIndex{ Task task, n ->
      println "${n + 1} $task $task.path" 
      task.dependsOn.eachWithIndex{ depObj, m ->
          println "  ${ m + 1 } $depObj"
      }
  }
}

